data "aws_ami" "ubuntu" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-jammy-22.04-amd64-server-*"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  owners = ["099720109477"] # Canonical
}


resource "aws_key_pair" "deployer" {
  key_name   = "${local.project}-backend-kp"
  public_key = file(var.public_key_path)
}

resource "aws_security_group" "network-security-group" {
  name        = "${local.project}-backend-sg"
  description = "Allow TLS inbound traffic"

  ingress {
    description = "SSH"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  egress {
    description = "Access to Internet"
    from_port   = 0
    to_port     = 65535
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = {
    Name = "${local.project}-backend-sg"
  }
}

resource "aws_instance" "api_backend" {
  ami           = data.aws_ami.ubuntu.id
  instance_type = "t3.micro"
  user_data     = templatefile("${path.root}/templates/cloud-init.yml.tpl", {
    run_script = filebase64("${path.root}/templates/run.sh")
  } )
  vpc_security_group_ids = [aws_security_group.network-security-group.id]
  key_name               = aws_key_pair.deployer.key_name
  tags                   = {
    Name = "${local.project}-backend-api"
  }
}